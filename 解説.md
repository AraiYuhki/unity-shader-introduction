# シェーダー入門 ～ポストエフェクト～

## 使用バージョン

- Unity2020.3.15f2
- VisualStudio2019

## 初めに

この資料は、シェーダー初心者向けのテクニック集になります。
また、Windows向けに解説しているので、Mac向けではありません。

## シェーダーを書くときに気をつけること

### 1.除算は可能な限り避ける

コンピュータは乗算は得意ですが除算は非常に苦手で、除算を使用するだけでパフォーマンスが非常に悪くなります。

例えば、`size / 2`などは`size * 0.5`と書き換える等、シンプルな除算は掛け算に変換しましょう。

`size / num`等、変数で割っている場合は仕方がないので、そのまま使います。

---

### 2.ピクセルごとに条件分岐するようなif文は避ける

GPUはif文で各スレッドの処理が変わってしまう場合、分岐先のすべてのパターンの処理を実行した後、スレッドごとに条件に当てはまる結果を選択して出力するという動作になっています。

なのでUVの値を参照に条件分岐するような処理はパフォーマンスが著しく悪化するため、基本的にif文は避けたほうが懸命です。

当然、if文の数に指数関数的に処理時間が増えるので、気をつけること。

詳しくは[こちら](https://qiita.com/HnniTns/items/9b0e18d52117d327d9c4)がわかりやすいです。

条件分岐は組み込み関数を利用した処理に置き換えましょう。

#### 置き換え表

|式|コード|
|:--|:--|
|x >= y|step(y, x)|
|x <= y|step(x, y)|
|x > y|1 - step(x, y)|
|x < y|1 - step(y, x)|
|x == y|1 - abs(sign(x - y))|
|x != y|abs(sign(x - y))|
|0 < a < 1|abs(sign(a - ceil(saturate(a))))|
|x < a < y|(1 - step(x, a)) * (1 - step(a, y))|
|0 <= a <= 1|1.0 - abs(sign(a - saturate(a)))|
|x <= a <= y|step(x, a) * step(a, y)|

---

### 3.浮動小数の変数は型に気を配る

Unityのシェーダー言語であるShaderLabには、用途に応じた3種類の浮動小数の型が用意されています。

- float 最高精度。C#などと同じように32bitの浮動小数点を表し、高精度な型です。一般的に空間座標やテクスチャ座標、三角法などで使用されます。
- half 中精度。16bitの浮動小数点です。–60000から+60000の範囲で、小数点以下約3桁)。ショートベクトル、方向、オブジェクト空間位置、HDR カラーなどに使用されます。
- fixed 低精度。-2.0～+2.0の範囲で、1/256の精度です。標準カラーやそれらの制御に使用され、一般的なテクスチャのピクセルはこの型で格納されています。

塵も積もれば山となるので、精度を求めない変数にfloatを使用するのは避けましょう。

---

### 4.ベクトルはできるだけベクトルで計算する

GPUはベクトルを処理するための命令が実装されているので、以下のような計算は非効率になります。

```hlsl
fixed4 col = tex2D(_MainTex, i.uv);
col.r += addR;
col.g += addG;
col.b += addB;
```

このような処理は以下のようにしてしまった方がパフォーマンスが出ます。

```hlsl
fixed4 col = tex2D(_MainTex, i.uv);
col.rgb += fixed3(addR, addG, addB);
```

行列計算も得意です。

---

### 5.テクスチャはRGBAすべてのチャンネルをフル活用する

これはモバイル向けのチューニングテクニックとなりますが、

複数のグレースケールのテクスチャを使用して処理をするようなシェーダーでは、

複数のテクスチャをメモリに展開するとかなりの量になってしまうので、大幅なメモリの節約になります。

---

## 実例～ポストエフェクト～

ベースとなるシェーダーコードは以下のものになります。

```hlsl
Shader "PostEffect/Base"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
    }
    SubShader
    {
        // No culling or depth
        Cull Off ZWrite Off ZTest Always

        Pass
        {
            CGPROGRAM
            #pragma vertex vert_img
            #pragma fragment frag

            #include "UnityCG.cginc"
            sampler2D _MainTex;

            // ここの中身を変更する。
            fixed4 frag (v2f_img i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv);
                return col;
            }
            ENDCG
        }
    }
}
```

制御用のプログラムは以下のものになります

```C#
using UnityEngine;
[RequireComponent(typeof(Camera))]
public class PostEffect : MonoBehaviour
{
    [SerializeField]
    protected Material material;

    // ここでポストエフェクトを適用する
    protected virtual void OnRenderImage(RenderTexture source, RenderTexture destination)
    {
        Graphics.Blit(source, destination, material);
    }
}
```

### 色調変換

```hlsl
// 色調変換
// ネガポジ反転
return 1 - col;
// グレースケール
col.rgb = Luminance(col.rgb);
return col;
// セピア
return fixed4(
    col.r * 0.393 + col.g * 0.769 + col.b * 0.189,
    col.r * 0.349 + col.g * 0.686 + col.b * 0.168,
    col.r * 0.272 + col.g * 0.534 + col.b * 0.131,
    col.a
);
```

### 色収差

カメラなどのレンズ越しで見たときに生じる、色の波長の違いによるRGBの色が僅かにずれて見える現象のこと。
[Wikipedia](https://ja.wikipedia.org/wiki/%E8%89%B2%E5%8F%8E%E5%B7%AE)

```hlsl
float4 _Offset; // xyがRのオフセット、zwがBのオフセット

fixed4 frag(v2f_img i) : SV_Target
{
    return fixed4(
        tex2D(_MainTex, i.uv + _Offset.xy).r,
        tex2D(_MainTex, i.uv).g,
        tex2D(_MainTex, i.uv + _Offset.zw).b,
        tex2D(_MainTex, i.uv).a
    );
}
```

### 深度値の取得

```hlsl
sampler2D _CameraDepthTexture; // 深度テクスチャ

fixed4 frag (v2f_img i) : SV_Target
{
    float depth = SAMPLE_DEPTH_TEXTURE(_CameraDepthTexture, i.uv);
    depth = Linear01Depth(depth); // near～farで0～1の線形補間の値にする
    // depth = LinearEyeDepth(depth); こちらはワールド空間上の距離として取得する
    return depth;
}
```

### 簡単な図形を描画してみる

ここでif文を使わずに条件分岐を行う方法も説明します。

#### 準備

UVを (0, 0) を中心地点に計算し直す処理

float4 {テクスチャ名}_TexelSize;

で指定したテクスチャの幅高さ等の情報を取得することができます。

内容は (x, y, z, w) = (1 / width, 1 / height, width, height)

```hlsl
float4 _MainTex_TexelSize;
float2 convertUV(float2 uv, float4 texelSize) {
    // uv * 2.0 - 1.0 で、0,0を中心に -1.0 ～ 1.0 に変換する
    // width / height を uv.xに掛けることでアスペクト比に対応した状態に変換できる。
    // y が　1 / height なので、除算を使わずに掛け算で処理する
    return (uv * 2.0 - 1.0) * float2(texelSize.z * texelSize.y, 1.0);
}
```

#### 円

```hlsl
float4 _Status; (中心のX座標, 中心のY座標, 半径, 未使用);
fixed4 frag(v2f_img i) : SV_Target
{
    float2 uv = convertUV(i.uv, _MainTex_TexelSize);
    float distance = distance(uv, _Status.xy);
    fixed value = step(distance, _Status.z); // 半径以下の距離の場合は1
    return (value, value, value, 1.0);
}
```

#### 矩形

正方形を望む場合は以下の正多角形の方を使う方が確実

```hlsl
float4 _Status; // (左上の頂点のX座標, 左上の頂点のY座標, 右下の頂点のX座標, 右下の頂点のY座標)
fixed4 frag(v2f_img i) : SV_Target
{
    fixed value = step(_Status.x, i.uv.x) * step(i.uv.x, _Status.z) * step(_Status.y, i.uv.y) * (i.uv.y, _Status.w);
    return fixed4(value, value, value, 1.0);
}
```

#### 正多角形

```hlsl
static const float PI = 3.14159265359;
static const float TWO_PI = 6.28318530718;
static const float DEG2RAD = 0.0174532924; // TWO_PI / 360
int _VertexCount;
float4 _Status; (中心のX座標, 中心のY座標, 半径, 回転角);
float4 _MainTex_TexelSize;
fixed4 frag(v2f_img i) : SV_Target
{
    float2 position = i.uv * 2.0 - 1.0;
 position += _Status.xy;
 position *= float2(_MainTex_TexelSize.z * _MainTex_TexelSize.y, 1.0);
 float a = atan2(position.x, position.y) + _Status.w * DEG2RAD;
 float r = TWO_PI / (float)_VertexCount;
 float d = cos(floor(0.5 + a / r) * r - a) * length(position);
    fixed value = step(d, _Status.z);
    return fixed4(value, value, value, 1.0);
}
```

### 画面全体を歪ませる

陽炎のようなエフェクトを画面全体に掛けます。

ここではテクスチャのRGBを別々の用途に利用したテクニックも使用します。

用意したテクスチャは、どのようにUV値をずらすかをRGBに分けて書き込んだものになります。

今回は下から上に行くほど歪み方が弱くなるようにし、位相の異なる雲模様を2つ重ねてそれらしく動くように調整しています。

```hlsl
Shader "Distortion/Texture"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _Distortion ("DistortionTex", 2D) = "gray" {} // (r, g, b, a) = (X軸のズレ, Y軸のズレ, ズレの補正値, 未使用) -1.0 ~ 1.0 の範囲で使用するため、デフォルトは灰色
        _Power ("Power", Range(0, 0.5)) = 0.02 // 歪みの強さ
        _ScrollSpeed ("ScrollSpeed", Vector) = (0.1, -0.1, -0.1, -0.1) // 歪みテクスチャをスクロールさせる (RのX軸の移動量, RのY軸の移動量, GのX軸方向の移動量, GのY軸方向の移動量)
    }
    SubShader
    {
        // No culling or depth
        Cull Off ZWrite Off ZTest Always

        Pass
        {
            CGPROGRAM
            #pragma vertex vert_img
            #pragma fragment frag

            #include "UnityCG.cginc"
            sampler2D _MainTex;
            sampler2D _Distortion;
            float _Power;
            float4 _ScrollSpeed;

            fixed4 frag(v2f_img i) : SV_Target
            {
                float power = tex2D(_Distortion, i.uv).b * _Power; // 全体の強さを決定したいので、外部から設定した歪み強度をかけ合わせる
                float offsetX = (tex2D(_Distortion, i.uv + _Time.y * _ScrollSpeed.xy).r - 0.5) * 2 * power; // X軸方向にずらす数値を計算
                float offsetY = (tex2D(_Distortion, i.uv + _Time.y * _ScrollSpeed.zw).g - 0.5) * 2 * power; // Y軸方向にずらす数値を計算
                fixed4 col = tex2D(_MainTex, i.uv + float2(offsetX, offsetY)); // UVをずらして画面のピクセルを取得。あぶれた分はClampのテクスチャと同じ処理がされるはず
                return col;
            }
            ENDCG
        }
    }
}

```

### ブラー

以降は制御プログラムも少し変更する必要が出てきます。

```hlsl
float4 _MainTex_TexelSize;
uniform half _BlurSize;
static const int BlurSampleCount = 8; // 参照するピクセル数
static const float BlurColorAverage = 0.125; // ↑で指定した数のピクセルの平均を取るため、1/BlurSampleCountの値を入れておく
// 参照するピクセルのUVオフセット
static const float2 BlurKernel[BlurSampleCount] = {
    float2(-1.0, -1.0),
    float2(-1.0, 1.0),
    float2(1.0, -1.0),
    float2(1.0, 1.0),
    float2(-0.70711, 0),
    float2(0, 0.7011),
    float2(0.70711, 0),
    float2(0, -0.70711)
};

fixed4 frag(v2f_img i) : SV_Target
{
    // ブラーの強さ
    float2 scale = _BlurSize * 0.001;
    // スクリーンサイズに合わせて補正を掛ける
    scale.y *= _MainTex_TexelSize.z * _MainTex_TexelSize.y;
    fixed4 color = 0;
    // 指定したピクセルから値を取得する
    for (int j = 0; j < BlurSampleCount; j++) {
        color += tex2D(_MainTex, i.uv + BlurKernel[j] * scale);
    }
    // 平均値を取る
    color.rgb *= BlurColorAverage;
    // アルファ値はそのまま
    color.a = tex2D(_MainTex, i.uv).a;
    return color;
}
```

```C#
using UnityEngine;
public class Blur : PostEffect
{
    protected override void OnRenderImage(RenderTexture source, RenderTexture destination)
    {
        // 縮小したテクスチャに書き込む
        // 縮小倍率を上げることでよりボケボケにすることができるので、自分の望む範囲で調整しましょう。
        var tmp = RenderTexture.GetTemporary(Screen.width / 2, Screen.height / 2, 0, source.format);
        var tmp2 = RenderTexture.GetTemporary(Screen.width / 4, Screen.height / 4, 0, source.format);
        Graphics.Blit(source, tmp, material);
        Graphics.Blit(tmp, tmp2, material);
        // 最後にぼかしたテクスチャを描画し、不要になったレンダーテクスチャを開放する
        Graphics.Blit(tmp2, destination);
        RenderTexture.ReleaseTemporary(tmp);
        RenderTexture.ReleaseTemporary(tmp2);
    }
}
```

### 被写界深度

焦点があっている箇所だけはっきりして、それ以外の部分はブラーがかかるというもの。

```hlsl
Shader "PostEffect/DepthOfField"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _BlurTex ("Blur", 2D) = "black" {}
        _Forcus ("Forcus", Range(0.0, 1.0)) = 0.5
        _ForcusRange("ForcusRange", Range(0.0, 0.5)) = 0.05
    }
    SubShader
    {
        // No culling or depth
        Cull Off
        ZWrite Off
        ZTest Always

        Pass
        {
            CGPROGRAM
            #pragma vertex vert_img
            #pragma fragment frag

            #include "UnityCG.cginc"
            sampler2D _MainTex;
            sampler2D _BlurTex;
            sampler2D _CameraDepthTexture;
            float _Forcus;
            float _ForcusRange;

            fixed4 frag (v2f_img i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv);
                fixed4 blur = tex2D(_BlurTex, i.uv);
                float depth = SAMPLE_DEPTH_TEXTURE(_CameraDepthTexture, i.uv);
                depth = Linear01Depth(depth);
                float distance = clamp(abs(depth - _Forcus), 0, 1);
                distance = smoothstep(0, _ForcusRange, distance);
                return lerp(col, blur, distance);
            }
            ENDCG
        }
    }
}

```

```C#
using UnityEngine;

public class DepthOfField : PostEffect
{
    [SerializeField]
    private Material blurMaterial;
    [SerializeField]
    private new Material material;

    private int blurTexId = -1;

    public void Start()
    {
        // 先にプロパティのIDを解決しておくことで、プロパティへのアクセスを高速化することができる。
        blurTexId = Shader.PropertyToID("_BlurTex");
    }

    protected override void OnRenderImage(RenderTexture source, RenderTexture destination)
    {
        // ブラーをかけたものを用意
        var tmp = RenderTexture.GetTemporary(Screen.width / 4, Screen.height / 4, 0, source.format);
        var tmp2 = RenderTexture.GetTemporary(Screen.width / 8, Screen.height / 8, 0, source.format);
        Graphics.Blit(source, tmp, blurMaterial);
        Graphics.Blit(tmp, tmp2, blurMaterial);

        material.SetTexture(blurTexId, tmp2);

        Graphics.Blit(source, destination, material);
        RenderTexture.ReleaseTemporary(tmp);
        RenderTexture.ReleaseTemporary(tmp2);
    }
}

```

#### おまけ ノイズ

シェーダーで乱数を使いたいときに使う計算式

ホワイトノイズなどを再現するときに有効です。

```hlsl
float random(fixed2 p) {
    return frac(sin(dot(p, fixed2(12.9898, 78.233))) * 43758.5453);
}

// パーリンノイズ
float perlinNoise(fixed2 uv) {
    fixed2 p = floor(uv);
    fixed2 f = frac(uv);
    fixed2 u = f * f * (3.0 - 2.0 * f);

    float v00 = random(p + fixed2(0, 0));
    float v10 = random(p + fixed2(1, 0));
    float v01 = random(p + fixed2(0, 1));
    float v11 = random(p + fixed2(1, 1));
    return lerp(lerp(dot(v00, f - fixed2(0, 0)), dot(v10, f - fixed2(1, 0)), u.x),
                lerp(dot(v01, f - fixed2(0, 1)), dot(v11, f - fixed2(1, 1)), u.x),
                u.y) + 0.5;
}
```

## 3Dオブジェクト用のシェーダー

### 通常のライティング(自他共に影を落とさない)

```hlsl
Shader "Custom/Deffuse"
{
    Properties
    {
        _Color ("Color", Color) = (1,1,1,1)
        _MainTex ("Albedo (RGB)", 2D) = "white" {}
    }
    SubShader
    {
        Tags {
            "RenderType"="Opaque"
            "LightMode"="ForwardBase"
        }
        Pass {
            Cull Back
            Blend SrcAlpha OneMinusSrcAlpha

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #include "Lighting.cginc"

            #pragma target 3.0

            sampler2D _MainTex;

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
                float3 normal : NORMAL;
            };

            struct v2f
            {
                float4 vertex : SV_POSITION;
                float2 uv : TEXCOORD0;
                float3 worldNormal : TEXCOORD1;
            };

            fixed4 _Color;

            v2f vert(appdata v)
            {
                v2f o;

                // 頂点をワールド座標に変換する
                // mul(UNITY_MATRIX_MVP, float4(v.vertex.xyz, 1.0))と等価
                // MVPは Model,View,Projection の行列をかけ合わせたもの。DirectXなどを直接使わない限りは知っている必要はありませんので、詳細は省きます。
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv;
                // 法線をワールド空間に合わせて変換する
                o.worldNormal = UnityObjectToWorldNormal(v.normal);

                return o;
            }

            fixed4 frag(v2f i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv) * _Color;
                // ディレクショナルライトの方向を取得
                float3 lightDir = _WorldSpaceLightPos0.xyz;
                // 法線を正規化
                float3 normal = normalize(i.worldNormal);
                // ライトと法線のベクトルの内積を取って、0～1の間に収める
                float nl = clamp(dot(normal, lightDir), 0, 1);
                // テクスチャカラーとライトカラーを掛けて、影の暗さを掛ける
                col.rgb = _LightColor0 * col.rgb * nl;
                return col;
            }
            ENDCG
        }
    }
}

```

### トゥーンシェーダー

プロパティに影マップを追加し、フラグメントシェーダーを少し変更することで簡単なトゥーン調に変更することができます。

トゥーンシェーダーとは、影色がスムーズに変化するのではなく、数段階にくっきり分かれるような表現のことを言います。

```hlsl
sampler2D _ShadowMap;

fixed4 frag(v2f i) : SV_Target
{
    fixed4 col = tex2D(_MainTex, i.uv) * _Color;
    float3 lightDir = _WorldSpaceLightPos0.xyz;
    float3 normal = normalize(i.worldNormal);
    float nl = clamp(dot(normal, lightDir), 0, 1);
    // ここで、影の倍率を掛ける代わりに、影用のテクスチャから色を取得する。
    fixed3 shadowColor = tex2D(_ToonShadow, float2(nl, 0));
    col.rgb = _LightColor0 * col.rgb * shadowColor;
    return col;
}
```

## 3Dオブジェクトのブレンドモードについて

### ここからはエフェクトなどの半透明を扱う場合についての補足説明となります

`Blend SrcAlpha OneMinusSrcAlpha` と書かれている部分を書き換えることで様々なブレンディングをすることができます。

ちなみにこれはよくある半透明に対応したアルファブレンディングです。

基本的な記法は以下のようになります。

`Blend [SrcFactor] [DestFactor]`

同時にこちらの設定もすることで、減算合成などより高度な設定をすることができます。

`BlendOp [BlendOp]`

また、Blendの引数などの設定は `[]` でプロパティをくくることで外から変更することができるようになっています。

このテクニックを使用することで、同じコードで違う合成方法のマテリアルを作成する際に、コードのコピーを作成する必要がなくなります。

そしてスクリプトから変更することもできるので、工夫次第で色々な効果を発生させることができるでしょう。

#### BlendModeの説明

|No|Enum名|内容|
|:--:|:--|:--|
|0|Zero|0|
|1|One|1|
|2|DstColor|フレームバッファの色|
|3|SrcColor|Shader出力の色|
|4|OneMinusDstColor|(1 - フレームバッファの色)|
|5|SrcAlpha|Shader出力のα値|
|6|OneMinusSrcColor|(1 - Shader出力の色)|
|7|DstAlpha|フレームバッファのα値|
|8|OneMinusDstAlpha|(1 - フレームバッファのα値)|
|9|SrcAlphaSaturate|min(Shader出力のα値, 1 - フレーム罰画のα値)|
|10|OneMinusSrcAlpha|(1 - Shader出力のα値)|

#### BlendOpの説明

※ DX9までで使用できるもののみ抜粋

|No|Enum名|内容|
|:--|:--|:--|
|0|Add|(SrcColor \* SrcFactor) + (DstColor \* DstFactor)|
|1|Subtract|(SrcColor \* SrcFactor) - (DstColor \* DstFactor)|
|2|ReverseSubtract|(DstColor \* DstFactor) - (SrcColor \* SrcFactor)|
|3|Min|min(SrcColor, DstColor) ※BlendのFactorは使用しない|
|4|Max|max(SrcColor, DstColor) ※BlendのFactorは使用しない|

#### よく使われるブレンドモードのテンプレート

|効果|SrcFactor|DestFactor|BlendOp|
|:--|:--|:--|:--|
|アルファブレンド|SrcAlpha|OneMinusSrcAlpha|Add|
|加算(覆い焼きリニア)|One|One|Add|
|スクリーン|SrcAlpha|One|Add|
|減算合成|SrcAlpha|One|ReverseSubtract|
|比較(明)|One|One|Max|
|比較(暗)|One|One|Min|
|乗算|Zero|SrcColor|Add|
|反転|OneMinusDstColor|Zero|Add|

---

#### ZTestとZWriteについて

ZTestはフレームバッファに書き込まれている深度値と、書き込もうとしているピクセルの深度値を比較して、条件を満たしている場合にのみ描画する設定です。

デフォルトでは `LEqual(4)` が設定されています。 

ちなみに描画順も影響するので、TagのQueueの値にも気をつけましょう。

Queueの値が小さいほど先に描画されます。

|No|CompreFunction|備考|
|:--|:--|:--|
|0|Disabled|プラットフォームごとに挙動が違うので非推奨|
|1|Never|何も書かない(使いみちはあるのだろうか?)|
|2|Less||
|3|Equal||
|4|LessEqual||
|5|Greater||
|6|NotEqual||
|7|GreaterEqual||
|8|Always|描画順の重なりで描画する(Queueの値が大きいほうが優先される)|

ZWriteは深度値を深度バッファに書き込むかどうかの設定です。

半透明のオブジェクトの場合はZWriteがOffにしないと後ろのオブジェクトまで透過してしまうことがあります。

### パーティクルを使用せずに炎のエフェクトを作成する

最後に低スペック向けに最適化した、簡易エフェクトを作成します。

ブレンドシェイプを利用したエフェクトで、4枚のテクスチャを1枚にまとめてメモリを節約するテクニックを使用しています。

```hlsl
Shader "Unlit/LquidLikeEffect"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {} // r:ベースの形状 g:ベースの形状に揺らぎを与えるマスク b:形状の中の模様
        _BaseColor ("BaseColor", Color) = (0, 0, 0, 1)
        _LineColor ("LineColor", Color) = (1, 1, 1, 1)
        _ScrollSpeed ("ScrollSpeed", Float) = 0.2
        _OutlineBlur ("OutlineBlur", Float) = 0.2
        [Enum(UnityEngine.Rendering.BlendMode)]
        _SrcFactor ("SrcFactor", Float) = 5
        [Enum(UnityEngine.Rendering.BlendMode)]
        _DstFactor ("DstFactor", Float) = 10
        [Enum(UnityEngine.Rendering.BrendOp)]
        _BlendOp ("BlendOp", Float) = 0
    }
    SubShader
    {
        Tags {
            "RenderType"="Transparent"
            "Queue"="Transparent"
        }
        ZWrite off
        Blend [_SrcFactor] [_DstFactor]
        BlendOp [_BlendOp]

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"
            #include "../libs/Utility.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                UNITY_FOG_COORDS(1)
                float4 vertex : SV_POSITION;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed4 _BaseColor;
            fixed4 _LineColor;
            float _ScrollSpeed;
            float _OutlineBlur;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                UNITY_TRANSFER_FOG(o,o.vertex);
                return o;
            }

            
            float4 frag(v2f i) : SV_Target
            {
                // ブレンドシェイプ
                fixed r = tex2D(_MainTex,i.uv).r;

                float scrollProgress = actualNumberDiscard(_Time.y, _ScrollSpeed);
                i.uv.y -= scrollProgress;
                fixed2 gb = tex2D(_MainTex, i.uv).gb;

                // α値を調整
                fixed shape = ((r * 2.0f) - 1.0f) + gb.x;
                shape = clamp(shape, 0, 1);

                // 内部で色を変動させる
                float4 col = float4(1.0, 1.0, 1.0, 1.0);
                col.rgb = lerp(_LineColor.rgb, _BaseColor.rgb, shape - gb.y);
                col.a = smoothstep(0.0f, _OutlineBlur, shape);

                return col;
            }
            ENDCG
        }
    }
}
```
